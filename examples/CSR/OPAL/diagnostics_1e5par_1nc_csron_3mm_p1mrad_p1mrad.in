//============================================================================
//
//  AWA EEX beamline input script
//  Edited on September 7th, 2022
//
//============================================================================

OPTION, PSDUMPFREQ = 100;	// How often 6d info is dumped to .h5
OPTION, STATDUMPFREQ = 10;	// How often beam stats dumped to .stat.  
OPTION, AUTOPHASE=0;		// Always leave this on, unless doing a phase scan
OPTION, VERSION=10900; 
Title, string="OPAL simulation for EEX beamline";
Value,{OPALVERSION};

//============================================================================
//Global Parameters
//============================================================================

REAL rf_freq         	     = 1.3e9;       	//[Hz] RF frequency
REAL n_particles             = 1E5  ;         	//Number of particles in simulation
REAL Q_bunch                 = 1e-9;   	    //[C] Charge of bunch
REAL CSR_ON                  = 1.0;             // CSR flag
STRING INITIAL_BEAM_FNAME = "./dogleg_beam_1e5_3mm_p1mrad_p1mrad.data" ; // filename of initial beam

//============================================================================
//Fieldsolver configuration
//============================================================================

REAL MX   = 8;
REAL MY   = 8;   
REAL MZ   = 8;
REAL BINS =  5;

//============================================================================
// Design momentum and energy
//============================================================================

REAL P0      = 43.4*1e6; //[eV/c]
REAL E0 = (P0^2 + (EMASS*1e9)^2)^0.5; // in [eV] unit
value , {E0, P0}; 

//============================================================================
// Dogleg lattice
//============================================================================

REAL DIP5_POS   = 0.3 ;
REAL DIP_E      = E0*1e-6; // In MeV unit
REAL DIP_LEN    = 0.3; // Length of the dipole magnet (Physical length)

if (CSR_ON == 1.0){

	CSR_FILTER: 
	FILTER,
	TYPE = "Savitzky-Golay",
	NPOINTS = 20, 
	NLEFT = 4,
	NRIGHT = 4,
	POLYORDER = 4;

	CSR_WK: 
		WAKE,
		TYPE="1D-CSR",
		NBIN=300,
		FILTERS=CSR_FILTER;

	EEXDIP5:	
		RBend, 	
		ANGLE = -20.00 * (Pi / 180.0), 
		FMAPFN = "./EEXDIP30cm.opal",
		ELEMEDGE = DIP5_POS, 
		DESIGNENERGY = DIP_E, 
		E1=0, 
		L = DIP_LEN, 
		GAP = 0.05, 
		WAKEF = CSR_WK;

}
else{

	EEXDIP5:	
		RBend, 	
		ANGLE = -20.00 * (Pi / 180.0), 
		FMAPFN = "./EEXDIP30cm.opal",
		ELEMEDGE = DIP5_POS, 
		DESIGNENERGY = DIP_E, 
		E1=0, 
		L = DIP_LEN, 
		GAP = 0.05;

}

// ================================================================================
// Beamline description (EYG not included)
// ================================================================================

MyLine: 
	Line = (EEXDIP5);

// ================================================================================
// Beam distribution
// ================================================================================

Dist: 
	DISTRIBUTION, 
	TYPE = FROMFILE,
    FNAME = INITIAL_BEAM_FNAME,
    EMITTED = False,    
    WRITETOFILE = True;

BEAM1: 
	BEAM, 
	PARTICLE = ELECTRON,
	pc = P0*1e-9, 
	NPART = n_particles, 
	BFREQ = rf_freq*1e-6,
	BCURRENT = Q_bunch*rf_freq, 
	CHARGE = -1;

// ================================================================================
// Define Field solvers
// ================================================================================

FS_SC: 
	Fieldsolver, 
	FSTYPE = NONE, 
	MX= MX, MY= MY, MT= MZ,
	PARFFTX = true, 
	PARFFTY = true, 
	PARFFTT = true,
	BCFFTX = open, 
	BCFFTY = open, 
	BCFFTT = open,
	BBOXINCR = 1, 
	GREENSF = INTEGRATED;

// ================================================================================
// Run beamline
// ================================================================================

TRACK, 
	LINE = MyLine, 
	BEAM = BEAM1, 
	MAXSTEPS = 1900000, 
	DT  = {1.0e-13, 1.0e-14, 1.0e-13},
	ZSTOP = {0.05, 0.8503, 0.3+0.3+1.0};

RUN, 
	METHOD = "PARALLEL-T", 
	BEAM = BEAM1, 
	FIELDSOLVER = FS_SC, 
	DISTRIBUTION = Dist;

ENDTRACK;

